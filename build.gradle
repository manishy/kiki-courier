plugins {
    id 'java'
    id 'jacoco'
}

group = 'org.KikiCourier'
version = '1.0-SNAPSHOT'

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        xml.destination = file("$buildDir/jacocoXml")
        csv.destination = file("$buildDir/jacocoCsv")
        html.destination = file("$buildDir/jacocoHtml")
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName = 'KikiCourier'
    version = null
    manifest {
        attributes 'Main-Class': 'org.KikiCourier.Main'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_ERROR"
    }
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent)
            println("${result.resultType} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped)")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}
